rem /**
rem  * DWC2.bbj
rem  * This program is the result of starting with the DWC1.bbj program and
rem  * making more DWC-specific changes to it, including:
rem  *   - Adding the window without specifying the x, y, w, and h parameters
rem  *   - Removing the window's title bar and setting it to be initially maximized
rem  *   - Adding the controls to the window without specifying the ID, x, y, w, and h parameters
rem  */

wnd! = BBjAPI().openSysGui("X0").addWindow("Hello BBj DWC", $01101083$)

wnd!.setPanelStyle("display","inline-grid")
wnd!.setPanelStyle("grid-template-columns","1fr 2fr")
wnd!.setPanelStyle("gap","5px")
wnd!.setPanelStyle("padding","15px")

st! = wnd!.addStaticText("First Name:")
ed_firstname! = wnd!.addEditBox("")
st! = wnd!.addStaticText("Last Name:")
ed_lastname! = wnd!.addEditBox("")

rem Use an icon from the tabular pool
rem @see https://tabler-icons.io/
rem https://dwc.style/docs/#/dwc/dwc-icon
btnIcon$ = "<dwc-icon name='speakerphone'></dwc-icon>"

rem Use an icon from the feather pool
rem @see https://feathericons.com/
rem btnIcon$ = "<dwc-icon name='speaker' pool='feather'></dwc-icon>"

rem Use an icon from the fontawesome-free pool
rem @see https://fontawesome.com/v5/search?m=free
rem btnIcon$ = "<dwc-icon name='fas-microphone-alt' pool='fa'></dwc-icon>"

btn! = wnd!.addButton("<html>Say Hello" + btnIcon$ +"</html>")

btn!.setStyle("grid-column","2")

btn!.setAttribute("expanse","xl")
btn!.setAttribute("theme","primary")

wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,"sayhello")

process_events

byebye: bye

sayhello:
    firstname$=ed_firstname!.getText()
    lastname$=ed_lastname!.getText()
    a=msgbox("Hello "+firstname$+" "+lastname$,64,"Hello World")
return
